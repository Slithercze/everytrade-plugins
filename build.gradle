import java.nio.charset.StandardCharsets

plugins {
    id "org.owasp.dependencycheck" version "$owaspDependencyCheckGradlePluginVersion"
    id "org.sonatype.gradle.plugins.scan" version "$sonatypeScanGradlePluginVersion"
    id "com.github.spotbugs" version "$spotbugsGradlePluginVersion"
}

dependencies {
    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:$findsecbugsVersion"
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    tasks.withType(JavaCompile) {
        project.sourceCompatibility = JavaVersion.VERSION_11
        project.targetCompatibility = JavaVersion.VERSION_11
        options.encoding = StandardCharsets.UTF_8
        options.incremental = true
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.6.1"
}

final File privatePropertyFile = new File(project.rootDir, 'private/gradle.properties')
if (privatePropertyFile.exists()) {
    final Properties privateProperties = new Properties()
    privatePropertyFile.withReader { BufferedReader reader ->
        privateProperties.load(reader)
        privateProperties.stringPropertyNames().each {
            project.ext[it] = privateProperties[it]
        }
    }
}


plugins {
    id 'java-library'
    id 'checkstyle'
}

group 'io.everytrade'
version projectVersion

configurations {
    pluginCompile
    pluginRuntime
    fatJar {
        extendsFrom pluginCompile
        extendsFrom pluginRuntime
        exclude group: "org.slf4j"
    }
    compileClasspath {
        extendsFrom pluginCompile
    }
}

dependencies {
    annotationProcessor "org.pf4j:pf4j:$pf4jVersion"

    compileOnly project(':plugin-api')
    compileOnly 'org.slf4j:slf4j-api:1.7.30'

    pluginCompile 'com.univocity:univocity-parsers:2.8.3'
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-core:$xchangeVersion"
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-kraken:$xchangeVersion"
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-binance:$xchangeVersion"
    pluginCompile 'org.knowm.xchange:xchange-bittrexV3:5.0.2'
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-bitstamp:$xchangeVersion"
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-coinmate:$xchangeVersion"
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-bitfinex:$xchangeVersion"
    pluginCompile "com.github.GENERALBYTESCOM.XChange:xchange-coinbasepro:$xchangeVersion"
}

compileJava {
    options.compilerArgs.addAll('-Apf4j.storageClassName=org.pf4j.processor.ServiceProviderExtensionStorage')
}

jar {
    manifest {
        attributes('Plugin-Id': 'everytrade-base')
        attributes('Plugin-Version': projectVersion)
        attributes('Plugin-Requires': '20200902') //required Everytrade version
        attributes('Plugin-Description': 'basic integrations plugin')
        attributes('Plugin-Provider': 'Everytrade.io')
        attributes('Plugin-License': 'GNU GPL 2 with Classpath Exception')
    }
    from {
        configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

checkstyle {
    toolVersion checkstyleVersion
}
plugins {
    id 'java-library'
}

group 'io.everytrade'
version projectVersion

dependencies {
    annotationProcessor "org.pf4j:pf4j:$pf4jVersion"

    compileOnly project(':plugin-api')
    compileOnly 'org.slf4j:slf4j-api:1.7.30'

    compileOnly 'com.univocity:univocity-parsers:2.8.3'
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-core:$xchangeVersion"
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-kraken:$xchangeVersion"
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-binance:$xchangeVersion"
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-bittrex:$xchangeVersion"
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-bitstamp:$xchangeVersion"
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-coinmate:$xchangeVersion"
    compileOnly "com.github.GENERALBYTESCOM.XChange:xchange-bitfinex:$xchangeVersion"
}

configurations.runtimeClasspath {
    exclude group: "org.slf4j"
}

compileJava {
    options.compilerArgs.addAll('-Apf4j.storageClassName=org.pf4j.processor.ServiceProviderExtensionStorage')
}

jar {
    manifest {
        attributes('Plugin-Id': 'everytrade-base')
        attributes('Plugin-Version': projectVersion)
        attributes('Plugin-Requires': '20200902') //required Everytrade version
        attributes('Plugin-Description': 'basic integrations plugin')
        attributes('Plugin-Provider': 'Everytrade.io')
        attributes('Plugin-License': 'GNU GPL 2 with Classpath Exception')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    //TODO: remove license.txt, etc.
}
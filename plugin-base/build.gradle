plugins {
    id 'java-library'
}

group 'io.everytrade'
version projectVersion

configurations {
    pluginCompile
    pluginRuntime
    fatJar {
        extendsFrom pluginCompile
        extendsFrom pluginRuntime
        exclude group: "org.slf4j"
    }
    compileClasspath {
        extendsFrom pluginCompile
    }
}

dependencies {
    annotationProcessor "org.pf4j:pf4j:$pf4jVersion"

    compileOnly project(':plugin-api')
    compileOnly 'org.slf4j:slf4j-api:1.7.30'

    pluginCompile 'com.univocity:univocity-parsers:2.8.3'
    //TODO Change it to GENERALBYTESCOM once PR (charvam-->GENERALBYTESCOM) will be done.
    pluginCompile "com.github.charvam.XChange:xchange-core:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-kraken:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-binance:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-bittrexV3:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-bitstamp:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-coinmate:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-bitfinex:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-coinbasepro:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-bitmex:268ec0acaa"
    pluginCompile "com.github.charvam.XChange:xchange-bitflyer:268ec0acaa"
    pluginCompile "com.github.charvam:V3-Open-API-SDK:b50b7f4ae522160a4d718523e719b48134d0c31c"
    //TODO Remove it once ResCU releases a newer version with updated dependency - due to security bugs in the older
    // version used by ResCU
    pluginCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
}

jar {
    manifest {
        attributes('Plugin-Id': 'everytrade-base')
        attributes('Plugin-Version': projectVersion)
         attributes('Plugin-Requires': requiredEverytradeVersion) //required Everytrade version
        attributes('Plugin-Description': 'basic integrations plugin')
        attributes('Plugin-Provider': 'Everytrade.io')
        attributes('Plugin-License': 'GNU GPL 2 with Classpath Exception')
    }
    from {
        configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
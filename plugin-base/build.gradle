plugins {
    id 'java-library'
}

group 'io.everytrade'
version projectVersion

configurations {
    pluginCompile
    pluginRuntime
    fatJar {
        extendsFrom pluginCompile
        extendsFrom pluginRuntime
        exclude group: "org.slf4j"
    }
    compileClasspath {
        extendsFrom pluginCompile
    }
    testImplementation {
        extendsFrom pluginCompile
    }
}

dependencies {
    annotationProcessor "org.pf4j:pf4j:$pf4jVersion"

    compileOnly project(':plugin-api')
    compileOnly 'org.slf4j:slf4j-api:1.7.30'
    pluginCompile "com.univocity:univocity-parsers:2.8.4"
    pluginCompile "org.knowm.xchange:xchange-kraken:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-binance:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bittrex:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitstamp:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-coinmate:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitfinex:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-coinbasepro:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitmex:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitflyer:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-core:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-huobi:$xchangeVersion"
    pluginCompile "com.github.charvam:V3-Open-API-SDK:b50b7f4ae522160a4d718523e719b48134d0c31c"

    pluginCompile "io.netty:netty-all:4.1.46.Final" //TODO: remove once okex fixes its dependencies
    pluginCompile "commons-beanutils:commons-beanutils:1.9.4" //TODO: remove once okex fixes its dependencies
    pluginCompile "org.apache.commons:commons-compress:1.19" //TODO: remove once okex fixes its dependencies
    pluginCompile "com.squareup.okhttp3:okhttp:3.12.1" //TODO: remove once okex fixes its dependencies
    pluginCompile "com.alibaba:fastjson:1.2.31" //TODO: remove once okex fixes its dependencies
    pluginCompile "org.springframework:spring-context:5.1.18.RELEASE" //TODO: remove once okex fixes its dependencies
    pluginCompile "com.squareup.retrofit2:retrofit:2.5.0" //TODO: remove once okex fixes its dependencies
    pluginCompile "com.squareup.retrofit2:adapter-rxjava:2.5.0" //TODO: remove once okex fixes its dependencies
    pluginCompile "com.squareup.retrofit2:converter-gson:2.5.0" //TODO: remove once okex fixes its dependencies
    pluginCompile "com.squareup.retrofit2:converter-scalars:2.5.0" //TODO: remove once okex fixes its dependencies

    //TODO Remove it once ResCU releases a newer version with updated dependency - due to security bugs in the older
    // version used by ResCU
    pluginCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.5.1'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation project(':plugin-api')
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

}

jar {
    manifest {
        attributes('Plugin-Id': 'everytrade-base')
        attributes('Plugin-Version': projectVersion)
        attributes('Plugin-Requires': requiredEverytradeVersion) //required Everytrade version
        attributes('Plugin-Description': 'basic integrations plugin')
        attributes('Plugin-Provider': 'Everytrade.io')
        attributes('Plugin-License': 'GNU GPL 2 with Classpath Exception')
    }
    from {
        configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}